import com.linkedin.gradle.DistributeTask

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.0"
    }
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

allprojects {
    group = GROUP_ID
}

ext {
    compileSdkVersion = 24
    buildToolsVersion = '24.0.2'
    minSdkVersion = 14
    targetSdkVersion = 24
    supportLibrariesVersion = '24.2.1'
}

artifactoryPublish.skip = true
artifactory {
    contextUrl = 'https://linkedin.jfrog.io/linkedin'
    publish {
        repoKey = 'test-butler'
        username = System.getenv('ARTIFACTORY_USER') ?: ""
        password = System.getenv('ARTIFACTORY_KEY') ?: ""

        defaults {
            publications 'apk', 'lib', 'ivyLib', 'ivyApk'
        }

        publishArtifacts = true
    }
    clientConfig.setIncludeEnvVars(false)
}

task distributeBuild(type: DistributeTask) {
    dependsOn ':artifactoryPublish', 'test-butler-app:artifactoryPublish', 'test-butler-library:artifactoryPublish'
}

import com.linkedin.gradle.DistributeTask

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.0"
    }
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

allprojects {
    group = GROUP_ID
}

ext {
    compileSdkVersion = 25
    buildToolsVersion = '25.0.2'
    minSdkVersion = 14
    targetSdkVersion = 25
    supportLibrariesVersion = '25.3.1'
}

artifactoryPublish.skip = true
artifactory {
    contextUrl = 'https://linkedin.jfrog.io/linkedin'
    publish {
        repository {
            repoKey = 'test-butler'
            username = System.getenv('ARTIFACTORY_USER') ?: ""
            password = System.getenv('ARTIFACTORY_KEY') ?: ""

            ivy {
                ivyLayout = "[organisation]/[module]/[revision]/[module]-[revision].ivy"
                artifactLayout = "[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
                mavenCompatible = true
            }
        }

        defaults {
            publications 'apk', 'lib', 'ivyLib', 'ivyApk'
        }

        publishArtifacts = true
    }
    clientConfig.setIncludeEnvVars(false)
}

task distributeBuild(type: DistributeTask) {
    dependsOn ':artifactoryPublish', 'test-butler-app:artifactoryPublish', 'test-butler-library:artifactoryPublish'
}

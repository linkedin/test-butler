apply plugin: 'com.android.application'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'ivy-publish'

version = VERSION_NAME

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.linkedin.android.testbutler"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName VERSION_NAME

        signingConfig signingConfigs.debug
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("keystore/platform.keystore")
            keyAlias "platform"

            storePassword "android"
            keyPassword "android"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        abortOnError true
        warningsAsErrors true
        disable 'OldTargetApi', 'GradleDependency', 'PrivateApi', 'WakelockTimeout'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    android.applicationVariants.matching { variant -> variant.name == 'debug'}.all { variant ->
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += variant.getCompileClasspath(null)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
publishing {
    publications {
        ivyPhysicalDeviceApk(IvyPublication) {
          from new org.gradle.api.internal.java.JavaLibrary(new org.gradle.api.internal.artifacts.publish.DefaultPublishArtifact(project.getName(), 'aar', 'aar', null, new Date(), new File("$buildDir/outputs/aar/${project.getName()}-release.aar"), assemble), project.configurations.default.getAllDependencies())
          artifact sourcesJar
          artifact javadocJar
        }

        physicalDeviceApk(MavenPublication) {
          from new org.gradle.api.internal.java.JavaLibrary(new org.gradle.api.internal.artifacts.publish.DefaultPublishArtifact(project.getName(), 'aar', 'aar', null, new Date(), new File("$buildDir/outputs/aar/${project.getName()}-release.aar"), assemble), project.configurations.default.getAllDependencies())

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    description = 'A companion apk for stabilizing physical Android devices during testing'
                    url 'https://github.com/linkedin/test-butler'
                    scm {
                        url 'https://github.com/linkedin/test-butler'
                        connection 'scm:git:git://github.com/linkedin/test-butler.git'
                        developerConnection 'https://github.com/linkedin/test-butler.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'com.linkedin'
                            name 'LinkedIn Corp'
                            email ''
                        }
                    }
                }
            }

            //Useful for inspecting maven artifacts in 'build/repo' after running './gradlew publish'
            repositories {
                maven {
                    url = "$rootProject.buildDir/repo"
                }
            }
        }
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation project(':test-butler-api')
    implementation project(':test-butler-app-core')
    compileOnly "androidx.annotation:annotation:${rootProject.ext.supportLibrariesVersion}"
    implementation "androidx.legacy:legacy-support-v4:${rootProject.ext.supportLibrariesVersion}"
}
